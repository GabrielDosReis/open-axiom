)abbrev domain RESRING ResidueRing
++ Author: P.Gianni
++ Date Created: December 1992
++ Date Last Updated:
++ Basic Functions: 
++ Related Constructors:
++ Also See:
++ AMS Classifications:
++ Keywords: 
++ References:
++ Description: ResidueRing is the quotient of a polynomial ring by  an ideal.
++ The ideal is given as a list of generators. The elements of the domain
++ are equivalence classes expressed in terms of reduced elements

ResidueRing(F,Expon,VarSet,FPol,LFPol) : Dom  == Body
 where
   F       :  Field
   Expon   :  OrderedAbelianMonoidSup
   VarSet  :  OrderedSet
   FPol    :  PolynomialCategory(F, Expon, VarSet)
   LFPol   :  List FPol
   
   Dom   == Join(CommutativeRing, Algebra F) with
     reduce   :   FPol -> $
     ++ reduce(f) produces the equivalence class of f in the residue ring
     coerce   :   FPol -> $
     ++ coerce(f) produces the equivalence class of f in the residue ring
     lift     :     $  -> FPol
     ++ lift(x) return the canonical representative of the equivalence class x
   Body  ==  add 
    --representation
      Rep:= FPol
      import GroebnerPackage(F,Expon,VarSet,FPol)
      relations:= groebner(LFPol)

      relations = [1]$(List FPol) => error "the residue ring is the zero ring"
    --declarations
      x,y: $
    --definitions
      0 == 0$Rep
      1 == 1$Rep
      reduce(f : FPol) : $ == normalForm(f,relations)
      coerce(f : FPol) : $ == normalForm(f,relations)
      lift x  == x :: Rep :: FPol
      x + y == x +$Rep y
      -x == -$Rep x
      x*y == normalForm(lift(x *$Rep y),relations)
      (n : Integer) * x == n *$Rep x
      (a : F) * x == a *$Rep x
      x = y == x =$Rep y
      characteristic()      == characteristic()$F
      coerce(x) : OutputForm == coerce(x)$Rep

