)abbrev domain ODR OrdinaryDifferentialRing
++ Author: Stephen M. Watt
++ Date Created: 1986
++ Date Last Updated: June 3, 1991
++ Basic Operations:
++ Related Domains:
++ Also See:
++ AMS Classifications:
++ Keywords: differential ring
++ Examples:
++ References:
++ Description:
++   This constructor produces an ordinary differential ring from
++   a partial differential ring by specifying a variable.

OrdinaryDifferentialRing(Kernels,R,var): DRcategory == DRcapsule where
    Kernels:SetCategory
    R: PartialDifferentialRing(Kernels)
    var : Kernels
    DRcategory == Join(BiModule(%,%), DifferentialRing, HomotopicTo R) with
        if R has Field then Field
    DRcapsule == R add
        n: Integer
        Rep == R
        coerce(u: R): % == per u
        coerce(p: %): R == rep p
        differentiate p == per differentiate(rep p, var)

        if R has Field then
            p / q     == per(rep(p) / rep(q))
            p ** n    == per(rep(p) ** n)
            inv(p)    == per inv rep p

