import Type
import Stream
)abbrev package YSTREAM ParadoxicalCombinatorsForStreams
++ Computation of fixed points of mappings on streams
++ Author: Burge, Watt (revised by Williamson)
++ Date Created: 1986
++ Date Last Updated: 21 October 1989
++ Keywords: stream, fixed point
++ Examples:
++ References:
ParadoxicalCombinatorsForStreams(A):Exports == Implementation where
  ++ This package implements fixed-point computations on streams.
  A  :   Type
  ST ==> Stream
  L  ==> List
  I  ==> Integer
 
  Exports ==> with
    Y: (ST A -> ST A) -> ST A
      ++ Y(f) computes a fixed point of the function f.
    Y: (L ST A -> L ST A,I) -> L ST A
      ++ Y(g,n) computes a fixed point of the function g, where g takes
      ++ a list of n streams and returns a list of n streams.
 
  Implementation ==> add
 
    Y f ==
      y : ST A := CONS(0$I,0$I)$Lisp
      j := f y
      RPLACA(y,frst j)$Lisp
      RPLACD(y,rst j)$Lisp
      y
 
    Y(g,n) ==
      x : L ST A := [CONS(0$I,0$I)$Lisp for i in 1..n]
      j := g x
      for xi in x for ji in j repeat
        RPLACA(xi,frst ji)$Lisp
        RPLACD(xi,rst ji)$Lisp
      x

