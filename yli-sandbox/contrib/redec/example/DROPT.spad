)abbrev domain DROPT DrawOption
++ Author: Stephen Watt
++ Date Created: 1 March 1990
++ Date Last Updated: 31 Oct 1990, Jim Wen
++ Basic Operations: adaptive, clip, title, style, toScale, coordinates,
++ pointColor, curveColor, colorFunction, tubeRadius, range, ranges,
++ var1Steps, var2Steps, tubePoints, unit
++ Related Constructors:
++ Also See:
++ AMS Classifications:
++ Keywords:
++ References:
++ Description: DrawOption allows the user to specify defaults for the 
++ creation and rendering of plots.

DrawOption(): Exports == Implementation where 
 RANGE ==> List Segment Float
 UNIT  ==> List Float
 PAL   ==> Palette
 POINT ==> Point(DoubleFloat)
 SEG   ==> Segment Float
 SF     ==> DoubleFloat
 SPACE3 ==> ThreeSpace(DoubleFloat)
 VIEWPT ==> Record( theta:SF, phi:SF, scale:SF, scaleX:SF, scaleY:SF, scaleZ:SF, deltaX:SF, deltaY:SF )

 Exports ==> SetCategory with
  adaptive : Boolean -> %
    ++ adaptive(b) turns adaptive 2D plotting on if b is true, or off if b is
    ++ false. This option is expressed in the form \spad{adaptive == b}.
  clip : Boolean -> %
    ++ clip(b) turns 2D clipping on if b is true, or off if b is false. This option
    ++ is expressed in the form \spad{clip == b}.
  viewpoint : VIEWPT -> %
    ++ viewpoint(vp) creates a viewpoint data structure corresponding to the list
    ++ of values.  The values are interpreted as [theta, phi, scale, scaleX, scaleY,
    ++ scaleZ, deltaX, deltaY].  This option is expressed in the form 
    ++ \spad{viewpoint == ls}.
  title  : String -> %
    ++ title(s) specifies a title for a plot by the indicated string s. This option
    ++ is expressed in the form \spad{title == s}.
  style  : String -> %
    ++ style(s) specifies the drawing style in which the graph will be plotted
    ++ by the indicated string s. This option is expressed in the form \spad{style == s}.
  toScale : Boolean -> %
    ++ toScale(b) specifies whether or not a plot is to be drawn to scale;
    ++ if b is true it is drawn to scale, if b is false it is not. This option
    ++ is expressed in the form \spad{toScale == b}.

  clip : List SEG -> %
    ++ clip([l]) provides ranges for user-defined clipping as specified
    ++ in the list l. This option is expressed in the form \spad{clip == [l]}.
  coordinates : (POINT -> POINT) -> % 
    ++ coordinates(p) specifies a change of coordinate systems of point p. 
    ++ This option is expressed in the form \spad{coordinates == p}.
  pointColor : Float -> %
    ++ pointColor(v) specifies a color, v, for 2D graph points. This option
    ++ is expressed in the form \spad{pointColor == v}.
  pointColor : PAL -> %
    ++ pointColor(p) specifies a color index for 2D graph points from the spadcolors
    ++ palette p. This option is expressed in the form \spad{pointColor == p}.
  curveColor : Float -> %
    ++ curveColor(v) specifies a color, v, for 2D graph curves. This option is expressed
    ++ in the form \spad{curveColor == v}.
  curveColor : PAL -> %
    ++ curveColor(p) specifies a color index for 2D graph curves from the 
    ++ spadcolors palette p. This option is expressed in the form \spad{curveColor ==p}.
  colorFunction : (SF -> SF) -> %
    ++ colorFunction(f(z)) specifies the color based upon the z-component of 
    ++ three dimensional plots. This option is expressed in the form \spad{colorFunction == f(z)}.
  colorFunction : ((SF,SF) -> SF) -> %
    ++ colorFunction(f(u,v)) specifies the color for three dimensional plots
    ++ as a function based upon the two parametric variables. This option is expressed
    ++ in the form \spad{colorFunction == f(u,v)}.
  colorFunction : ((SF,SF,SF) -> SF) -> %
    ++ colorFunction(f(x,y,z)) specifies the color for three dimensional 
    ++ plots as a function of x, y, and z coordinates. This option is expressed in the
    ++ form \spad{colorFunction == f(x,y,z)}.
  tubeRadius : Float -> %
    ++ tubeRadius(r) specifies a radius, r, for a tube plot around a 3D curve;
    ++ is expressed in the form \spad{tubeRadius == 4}.
  range : List SEG -> %
    ++ range([l]) provides a user-specified range l. This option is expressed in the
    ++ form \spad{range == [l]}.
  range : List Segment Fraction Integer -> %
    ++ range([i]) provides a user-specified range i. This option is expressed in the
    ++ form \spad{range == [i]}.

  ranges : RANGE -> %
    ++ ranges(l) provides a list of user-specified ranges l. This option is expressed
    ++ in the form \spad{ranges == l}.
  space : SPACE3 -> %
    ++ space specifies the space into which we will draw.  If none is given
    ++ then a new space is created.
  var1Steps : PositiveInteger -> %
    ++ var1Steps(n) indicates the number of subdivisions, n, of the first 
    ++ range variable. This option is expressed in the form \spad{var1Steps == n}.
  var2Steps : PositiveInteger -> %
    ++ var2Steps(n) indicates the number of subdivisions, n,  of the second 
    ++ range variable. This option is expressed in the form \spad{var2Steps == n}.
  tubePoints : PositiveInteger -> %
    ++ tubePoints(n) specifies the number of points, n, defining the circle
    ++ which creates the tube around a 3D curve, the default is 6. This option is 
    ++ expressed in the form \spad{tubePoints == n}.
  coord : (POINT->POINT) -> %
    ++ coord(p) specifies a change of coordinates of point p. This option is expressed
    ++ in the form \spad{coord == p}.
  unit   : UNIT -> %
    ++ unit(lf) will mark off the units according to the indicated list lf.
    ++ This option is expressed in the form \spad{unit == [f1,f2]}.
  option : (List %, Symbol) -> Union(Any, "failed")
    ++ option() is not to be used at the top level; 
    ++ option determines internally which drawing options are indicated in 
    ++ a draw command.
  option?: (List %, Symbol) -> Boolean
    ++ option?() is not to be used at the top level;
    ++ option? internally returns true for drawing options which are 
    ++ indicated in a draw command, or false for those which are not.
 Implementation ==> add
  import AnyFunctions1(String)
  import AnyFunctions1(Segment Float)
  import AnyFunctions1(VIEWPT)
  import AnyFunctions1(List Segment Float)
  import AnyFunctions1(List Segment Fraction Integer)
  import AnyFunctions1(List Integer)
  import AnyFunctions1(PositiveInteger)
  import AnyFunctions1(Boolean)
  import AnyFunctions1(RANGE)
  import AnyFunctions1(UNIT)
  import AnyFunctions1(Float)
  import AnyFunctions1(POINT -> POINT)
  import AnyFunctions1(SF -> SF)
  import AnyFunctions1((SF,SF) -> SF)
  import AnyFunctions1((SF,SF,SF) -> SF)
  import AnyFunctions1(POINT)
  import AnyFunctions1(PAL)
  import AnyFunctions1(SPACE3)

  Rep := Record(keyword:Symbol, value:Any)

  length:List SEG -> NonNegativeInteger
  -- these lists will become tuples in a later version
  length tup == # tup

  lengthR:List Segment Fraction Integer -> NonNegativeInteger
  -- these lists will become tuples in a later version
  lengthR tup == # tup

  lengthI:List Integer -> NonNegativeInteger
  -- these lists will become tuples in a later version
  lengthI tup == # tup

  viewpoint vp == 
    ["viewpoint"::Symbol, vp::Any]

  title s == ["title"::Symbol, s::Any]
  style s == ["style"::Symbol, s::Any]
  toScale b == ["toScale"::Symbol, b::Any]
  clip(b:Boolean) == ["clipBoolean"::Symbol, b::Any]
  adaptive b == ["adaptive"::Symbol, b::Any]

  pointColor(x:Float) == ["pointColorFloat"::Symbol, x::Any]
  pointColor(c:PAL) == ["pointColorPalette"::Symbol, c::Any]
  curveColor(x:Float) == ["curveColorFloat"::Symbol, x::Any]
  curveColor(c:PAL) == ["curveColorPalette"::Symbol, c::Any]
  colorFunction(f:SF -> SF) == ["colorFunction1"::Symbol, f::Any]
  colorFunction(f:(SF,SF) -> SF) == ["colorFunction2"::Symbol, f::Any]
  colorFunction(f:(SF,SF,SF) -> SF) == ["colorFunction3"::Symbol, f::Any]
  clip(tup:List SEG) == 
    length tup > 3 =>
      error "clip: at most 3 segments may be specified"
    ["clipSegment"::Symbol, tup::Any]
  coordinates f == ["coordinates"::Symbol, f::Any]
  tubeRadius x == ["tubeRadius"::Symbol, x::Any]
  range(tup:List Segment Float) == 
    ((n := length tup) > 3) =>
      error "range: at most 3 segments may be specified"
    n < 2 =>
      error "range: at least 2 segments may be specified"
    ["rangeFloat"::Symbol, tup::Any]
  range(tup:List Segment Fraction Integer) == 
    ((n := lengthR tup) > 3) =>
      error "range: at most 3 segments may be specified"
    n < 2 =>
      error "range: at least 2 segments may be specified"
    ["rangeRat"::Symbol, tup::Any]

  ranges s               == ["ranges"::Symbol, s::Any]
  space s                == ["space"::Symbol, s::Any]
  var1Steps s            == ["var1Steps"::Symbol, s::Any]
  var2Steps s            == ["var2Steps"::Symbol, s::Any]
  tubePoints s           == ["tubePoints"::Symbol, s::Any]
  coord s                == ["coord"::Symbol, s::Any]
  unit s                 == ["unit"::Symbol, s::Any]
  coerce(x:%):OutputForm == x.keyword::OutputForm = x.value::OutputForm
  x:% = y:%              == x.keyword = y.keyword and x.value = y.value

  option?(l, s) ==
    for x in l repeat
      x.keyword = s => return true
    false

  option(l, s) ==
    for x in l repeat
      x.keyword = s => return(x.value)
    "failed"

