)abbrev domain ARITY Arity
++ Author: Gabriel Dos Reis
++ Date Created: December 04, 2008
++ Date Last Updated: December 04, 2008
++ Description:
++   This domain implements the arity of a function or an operator,
++   e.g. the number of arguments that an operator can take.  An 
++   arity is either a definition nonnegative integer, and the special
++   value `arbitrary', signifying that an operation can take any
++   number of arguments.
Arity(): Public == Private where
  Public == Join(SetCategory, RetractableTo NonNegativeInteger) with
    arbitrary: %
      ++ aribitrary is the arity of a function that accepts any
      ++ number of arguments.
  Private == add
    Rep == SingleInteger
    arbitrary == per(-1)
    hash x == hash rep x
    x = y == rep x = rep y
    coerce(x: %): OutputForm == 
      x = arbitrary => 'arbitrary::OutputForm
      rep(x)::OutputForm
    coerce(n: NonNegativeInteger): % == 
      max()$SingleInteger < n => 
        error "arity is too large for OpenAxiom"
      per(n::SingleInteger)
    retractIfCan x ==
      (n := rep x) < 0 => "failed"
      n::Integer::NonNegativeInteger
   
