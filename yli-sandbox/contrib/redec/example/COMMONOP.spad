)abbrev package COMMONOP CommonOperators
++ Provides commonly used operators
++ Author: Manuel Bronstein
++ Date Created: 25 Mar 1988
++ Date Last Updated: 2 December 1994
++ Description:
++ This package exports the elementary operators, with some semantics
++ already attached to them. The semantics that is attached here is not
++ dependent on the set in which the operators will be applied.
++ Keywords: operator.
CommonOperators(): Exports == Implementation where
  OP  ==> BasicOperator
  O   ==> OutputForm
  POWER ==> "%power"::Symbol
  ALGOP ==> "%alg"
  EVEN  ==> "even"
  ODD   ==> "odd"
  DUMMYVAR ==> "%dummyVar"

  Exports ==> with
    operator: Symbol -> OP
        ++ operator(s) returns an operator with name s, with the
        ++ appropriate semantics if s is known. If s is not known,
        ++ the result has no semantics.

  Implementation ==> add
    dpi        : List O -> O
    dgamma     : List O -> O
    dquote     : List O -> O
    dexp       : O -> O
    dfact      : O -> O
    startUp    : Boolean -> Void
    setDummyVar: (OP, NonNegativeInteger) -> OP

    brandNew?:Reference(Boolean) := ref true

    opalg   := operator("rootOf"::Symbol, 2)$OP
    oproot  := operator("nthRoot"::Symbol, 2)$OP
    oppi    := operator("pi"::Symbol, 0)$OP
    oplog   := operator("log"::Symbol, 1)$OP
    opexp   := operator("exp"::Symbol, 1)$OP
    opabs   := operator("abs"::Symbol, 1)$OP
    opsin   := operator("sin"::Symbol, 1)$OP
    opcos   := operator("cos"::Symbol, 1)$OP
    optan   := operator("tan"::Symbol, 1)$OP
    opcot   := operator("cot"::Symbol, 1)$OP
    opsec   := operator("sec"::Symbol, 1)$OP
    opcsc   := operator("csc"::Symbol, 1)$OP
    opasin  := operator("asin"::Symbol, 1)$OP
    opacos  := operator("acos"::Symbol, 1)$OP
    opatan  := operator("atan"::Symbol, 1)$OP
    opacot  := operator("acot"::Symbol, 1)$OP
    opasec  := operator("asec"::Symbol, 1)$OP
    opacsc  := operator("acsc"::Symbol, 1)$OP
    opsinh  := operator("sinh"::Symbol, 1)$OP
    opcosh  := operator("cosh"::Symbol, 1)$OP
    optanh  := operator("tanh"::Symbol, 1)$OP
    opcoth  := operator("coth"::Symbol, 1)$OP
    opsech  := operator("sech"::Symbol, 1)$OP
    opcsch  := operator("csch"::Symbol, 1)$OP
    opasinh := operator("asinh"::Symbol, 1)$OP
    opacosh := operator("acosh"::Symbol, 1)$OP
    opatanh := operator("atanh"::Symbol, 1)$OP
    opacoth := operator("acoth"::Symbol, 1)$OP
    opasech := operator("asech"::Symbol, 1)$OP
    opacsch := operator("acsch"::Symbol, 1)$OP
    opbox   := operator("%box"::Symbol)$OP
    oppren  := operator("%paren"::Symbol)$OP
    opquote := operator("applyQuote"::Symbol)$OP
    opdiff  := operator("%diff"::Symbol, 3)$OP
    opsi    := operator("Si"::Symbol, 1)$OP
    opci    := operator("Ci"::Symbol, 1)$OP
    opei    := operator("Ei"::Symbol, 1)$OP
    opli    := operator("li"::Symbol, 1)$OP
    operf   := operator("erf"::Symbol, 1)$OP
    opli2   := operator("dilog"::Symbol, 1)$OP
    opGamma     := operator("Gamma"::Symbol, 1)$OP
    opGamma2    := operator("Gamma2"::Symbol, 2)$OP
    opBeta      := operator("Beta"::Symbol, 2)$OP
    opdigamma   := operator("digamma"::Symbol, 1)$OP
    oppolygamma := operator("polygamma"::Symbol, 2)$OP
    opBesselJ   := operator("besselJ"::Symbol, 2)$OP
    opBesselY   := operator("besselY"::Symbol, 2)$OP
    opBesselI   := operator("besselI"::Symbol, 2)$OP
    opBesselK   := operator("besselK"::Symbol, 2)$OP
    opAiryAi    := operator("airyAi"::Symbol,  1)$OP
    opAiryBi    := operator("airyBi"::Symbol , 1)$OP
    opint   := operator("integral"::Symbol, 3)$OP
    opdint  := operator("%defint"::Symbol, 5)$OP
    opfact  := operator("factorial"::Symbol, 1)$OP
    opperm  := operator("permutation"::Symbol, 2)$OP
    opbinom := operator("binomial"::Symbol, 2)$OP
    oppow   := operator(POWER, 2)$OP
    opsum   := operator("summation"::Symbol, 3)$OP
    opdsum  := operator("%defsum"::Symbol, 5)$OP
    opprod  := operator("product"::Symbol, 3)$OP
    opdprod := operator("%defprod"::Symbol, 5)$OP

    algop   := [oproot, opalg]$List(OP)
    rtrigop := [opsin, opcos, optan, opcot, opsec, opcsc,
                         opasin, opacos, opatan, opacot, opasec, opacsc]$List(OP)
    htrigop := [opsinh, opcosh, optanh, opcoth, opsech, opcsch,
                   opasinh, opacosh, opatanh, opacoth, opasech, opacsch]$List(OP)
    trigop  := concat(rtrigop, htrigop)
    elemop  := concat(trigop, [oppi, oplog, opexp])
    primop  := [opei, opli, opsi, opci, operf, opli2, opint, opdint]
    combop  := [opfact, opperm, opbinom, oppow,
                                         opsum, opdsum, opprod, opdprod]
    specop  := [opGamma, opGamma2, opBeta, opdigamma, oppolygamma, opabs,
                opBesselJ, opBesselY, opBesselI, opBesselK]
    anyop   := [oppren, opdiff, opbox, opquote]
    allop   := concat(concat(concat(concat(concat(
                            algop,elemop),primop),combop),specop),anyop)

-- odd and even operators, must be maintained current!
    evenop := [opcos, opsec, opcosh, opsech, opabs]
    oddop  := [opsin, opcsc, optan, opcot, opasin, opacsc, opatan,
               opsinh, opcsch, optanh, opcoth, opasinh, opacsch,opatanh,opacoth,
                opsi, operf]

-- operators whose second argument is a dummy variable
    dummyvarop1 := [opdiff,opalg, opint, opsum, opprod]
-- operators whose second and third arguments are dummy variables
    dummyvarop2 := [opdint, opdsum, opdprod]

    operator s ==
      if (deref brandNew?) then startUp false
      for op in allop repeat
        is?(op, s) => return copy op
      operator(s)$OP

    dpi l    == "%pi"::Symbol::O
    dfact x  == postfix("!"::Symbol::O, (ATOM(x)$Lisp => x; paren x))
    dquote l == prefix(quote(first(l)::O), rest l)
    dgamma l == prefix(hconcat("|"::Symbol::O, overbar(" "::Symbol::O)), l)
    setDummyVar(op, n) == setProperty(op, DUMMYVAR, n pretend None)

    dexp x ==
      e := "%e"::Symbol::O
      x = 1::O => e
      e ** x

    startUp b ==
      brandNew?() := b
      display(oppren, paren)
      display(opbox, commaSeparate)
      display(oppi, dpi)
      display(opexp, dexp)
      display(opGamma, dgamma)
      display(opGamma2, dgamma)
      display(opfact, dfact)
      display(opquote, dquote)
      display(opperm, supersub("A"::Symbol::O, #1))
      display(opbinom, binomial(first #1, second #1))
      display(oppow, first(#1) ** second(#1))
      display(opsum,   sum(first #1, second #1, third #1))
      display(opprod, prod(first #1, second #1, third #1))
      display(opint, int(first #1 * hconcat("d"::Symbol::O, second #1),
                                                   empty(), third #1))
      input(oppren, convert concat(convert("("::Symbol)@InputForm,
                            concat(#1, convert(")"::Symbol)@InputForm)))
      input(oppow, convert concat(convert("**"::Symbol)@InputForm, #1))
      input(oproot,
            convert [convert("**"::Symbol)@InputForm, first #1, 1 / second #1])
      for op in algop   repeat assert(op, ALGOP)
      for op in rtrigop repeat assert(op, "rtrig")
      for op in htrigop repeat assert(op, "htrig")
      for op in trigop  repeat assert(op, "trig")
      for op in elemop  repeat assert(op, "elem")
      for op in primop  repeat assert(op, "prim")
      for op in combop  repeat assert(op, "comb")
      for op in specop  repeat assert(op, "special")
      for op in anyop   repeat assert(op, "any")
      for op in evenop  repeat assert(op, EVEN)
      for op in oddop   repeat assert(op, ODD)
      for op in dummyvarop1 repeat setDummyVar(op, 1)
      for op in dummyvarop2 repeat setDummyVar(op, 2)
      assert(oppren, "linear")
      void

