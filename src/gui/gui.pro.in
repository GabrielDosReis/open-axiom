## Copyright (C) 2011-2013 Gabriel Dos Reis.
## All rights reserved.
## Autoconf template file for the benefit of QMake 
## Written by Gabriel Dos Reis.

oa_targetdir = @top_builddir@/@target@

OA_INC = $${oa_targetdir}/include
OA_LIB = -L@top_builddir@/@target@/lib -lOpenAxiom -lopen-axiom-core

## We build in release mode.
CONFIG += release
## But, don't think we want an app bundle, yet.
macx {
  CONFIG -= app_bundle
}

macx-g++ {
  QMAKE_CFLAGS -= -Xarch_x86_64
  QMAKE_CFLAGS_X86_64 -= -Xarch_x86_64

  QMAKE_CXXFLAGS -= -Xarch_x86_64
  QMAKE_CXXFLAGS += -stdlib=libc++
  QMAKE_CXXFLAGS_X86_64 -= -Xarch_x86_64
  QMAKE_CXXFLAGS_X86_64 += -stdlib=libc++

  QMAKE_LFLAGS -= -Xarch_x86_64
  QMAKE_LFLAGS += -stdlib=libc++
  QMAKE_LFLAGS_X86_64 -= -Xarch_x86_64
  QMAKE_LFLAGS_X86_64 += -stdlib=libc++
}
QMAKE_MACOSX_DEPLOYMENT_TARGET = 10.7

## We are building a GUI application.
TEMPLATE = app

QT += core gui
greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

## Put the executable directly in the staged bin directory.
DESTDIR = @top_builddir@/@target@/bin
TARGET = open-axiom

## Tell QT that sources are not in the build directory
VPATH += @srcdir@

## Our headers
HEADERS += server.h conversation.h main-window.h debate.h
INCLUDEPATH += $$OA_INC
INCLUDEPATH +=  @srcdir@
INCLUDEPATH += @top_srcdir@/src/include
INCLUDEPATH += @top_builddir@/config
DEPENDPATH += @srcdir@

## Source files
SOURCES += server.cc conversation.cc main-window.cc debate.cc main.cc

## Additional support libraries
LIBS += $$OA_LIB

## C++ compiler
QMAKE_CXX = @CXX@
QMAKE_CXXFLAGS += -std=c++20 @oa_extra_cxxflags@
QMAKE_LINK = @CXX@
