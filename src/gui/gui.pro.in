## Copyright (C) 2011-2013 Gabriel Dos Reis.
## All rights reserved.
## Autoconf template file for the benefit of QMake 
## Written by Gabriel Dos Reis.

oa_targetdir = @top_builddir@/@target@

OA_INC = $${oa_targetdir}/include
OA_LIB = -L@top_builddir@/@target@/lib -lOpenAxiom
OA_IOLIB = -L@top_builddir@/src/io -lio
OA_SYNTAX_LIB = -L@top_builddir@/src/syntax -lsyntax
OA_RT_LIB = -L@top_builddir@/src/rt -lruntime

## We build in release mode.
CONFIG += release
## But, don't think we want an app bundle, yet.
macx {
  CONFIG -= app_bundle
}

macx-g++ {
  QMAKE_CFLAGS -= -Xarch_x86_64
  QMAKE_CFLAGS_X86_64 -= -Xarch_x86_64

  QMAKE_CXXFLAGS -= -Xarch_x86_64
  QMAKE_CXXFLAGS_X86_64 -= -Xarch_x86_64

  QMAKE_LFLAGS -= -Xarch_x86_64
  QMAKE_LFLAGS_X86_64 -= -Xarch_x86_64
}

## We are building a GUI application.
TEMPLATE = app

## Put the executable directly in the staged bin directory.
DESTDIR = @top_builddir@/@target@/bin
TARGET = open-axiom

## Tell QT that sources are not in the build directory
VPATH += @srcdir@

## Our headers
HEADERS += server.h conversation.h main-window.h debate.h
INCLUDEPATH += $$OA_INC
INCLUDEPATH +=  @srcdir@
INCLUDEPATH += @top_srcdir@/src/include
INCLUDEPATH += @top_builddir@/config
DEPENDPATH += @srcdir@

## Source files
SOURCES += server.cc conversation.cc main-window.cc debate.cc main.cc

## Additional support libraries
LIBS += $$OA_RT_LIB $$OA_SYNTAX_LIB $$OA_LIB $$OA_IOLIB

## C++ compiler
QMAKE_CXX = @CXX@
QMAKE_CXXFLAGS += -std=c++11
QMAKE_LINK = @CXX@
